cmake_minimum_required(VERSION 3.15)
project(NetHack VERSION 3.7)

if(CMAKE_BUILD_TYPE)
  message("Build type: ${CMAKE_BUILD_TYPE}.")
endif()

message(STATUS "Building NetHack version: ${NetHack_VERSION}.")

set(HACKDIR
    "$ENV{HOME}/nethackdir.37"
    CACHE STRING "Configuration files for nethack")

message(STATUS "HACKDIR set to: ${HACKDIR}")

include(ExternalProject)

# Playground vars
set(VARDIR ${HACKDIR})
set(INSTDIR ${HACKDIR})

add_compile_definitions(
  GCC_WARN
  NOCLIPPING
  NOMAIL
  NOTPARMDECL
  AVOID_WIN_IOCTL
  SCORE_ON_BOTL
  # ANSI_DEFAULT NO_SIGNAL
  HACKDIR="${HACKDIR}"
  DEFAULT_WINDOW_SYS="tty"
  DLB)

set(NH_SRC ${NetHack_SOURCE_DIR}/src)
set(NH_INC ${NetHack_SOURCE_DIR}/include)
set(NH_DAT ${NetHack_SOURCE_DIR}/dat)
set(NH_UTIL ${NetHack_SOURCE_DIR}/util)
set(NH_DOC ${NetHack_SOURCE_DIR}/doc)

set(NH_SRC_GEN ${NetHack_BINARY_DIR}/src)
set(NH_INC_GEN ${NetHack_BINARY_DIR}/include)
set(NH_DAT_GEN ${NetHack_BINARY_DIR}/dat)
set(NH_UTIL_GEN ${NetHack_BINARY_DIR}/util)

# Lua.
set(LUA_VERSION 5.4.3)

ExternalProject_Add(
  fetch-lua
  PREFIX "lua"
  URL http://www.lua.org/ftp/lua-${LUA_VERSION}.tar.gz
  URL_HASH
    SHA512=3a1a3ee8694b72b4ec9d3ce76705fe179328294353604ca950c53f41b41161b449877d43318ef4501fee44ecbd6c83314ce7468d7425ba9b2903c9c32a28bbc0
  BUILD_IN_SOURCE TRUE
  CONFIGURE_COMMAND ""
  BUILD_COMMAND make
  BUILD_BYPRODUCTS <BINARY_DIR>/src/liblua.a
  INSTALL_COMMAND ""
  STEP_TARGETS build)

ExternalProject_Get_Property(fetch-lua BINARY_DIR)
set(LUA_INCLUDE ${BINARY_DIR}/src)
set(TOPLUALIB ${BINARY_DIR}/src/liblua.a)

add_custom_command(
  OUTPUT ${NH_INC_GEN}/nhlua.h
  COMMAND ${CMAKE_COMMAND} -E echo "/* nhlua.h - generated by top Makefile */" >
          ${NH_INC_GEN}/nhlua.h
  COMMAND ${CMAKE_COMMAND} -E echo "#include \"lua.h\"" >> ${NH_INC_GEN}/nhlua.h
  COMMAND ${CMAKE_COMMAND} -E echo "#include \"lualib.h\"" >>
          ${NH_INC_GEN}/nhlua.h
  COMMAND ${CMAKE_COMMAND} -E echo "#include \"lauxlib.h\"" >>
          ${NH_INC_GEN}/nhlua.h
  COMMAND ${CMAKE_COMMAND} -E echo "'/*nhlua.h*/" >> ${NH_INC_GEN}/nhlua.h
  VERBATIM)

add_custom_target(lua_support DEPENDS fetch-lua-build ${NH_INC_GEN}/nhlua.h)

add_subdirectory(util)
add_subdirectory(dat)

set(NETHACK_SRC
    "src/allmain.c"
    "src/alloc.c"
    "src/apply.c"
    "src/artifact.c"
    "src/attrib.c"
    "src/ball.c"
    "src/bones.c"
    "src/botl.c"
    "src/cmd.c"
    "src/date.c"
    "src/dbridge.c"
    "src/decl.c"
    "src/detect.c"
    "src/dig.c"
    "src/display.c"
    "src/dlb.c"
    "src/do.c"
    "src/do_name.c"
    "src/do_wear.c"
    "src/dog.c"
    "src/dogmove.c"
    "src/dokick.c"
    "src/dothrow.c"
    "src/drawing.c"
    "src/dungeon.c"
    "src/eat.c"
    "src/end.c"
    "src/engrave.c"
    "src/exper.c"
    "src/explode.c"
    "src/extralev.c"
    "src/files.c"
    "src/fountain.c"
    "src/hack.c"
    "src/hacklib.c"
    "src/insight.c"
    "src/invent.c"
    "src/isaac64.c"
    "src/light.c"
    "src/lock.c"
    "src/mail.c"
    "src/makemon.c"
    "src/mcastu.c"
    "src/mdlib.c"
    "src/mhitm.c"
    "src/mhitu.c"
    "src/minion.c"
    "src/mklev.c"
    "src/mkmap.c"
    "src/mkmaze.c"
    "src/mkobj.c"
    "src/mkroom.c"
    "src/mon.c"
    "src/mondata.c"
    "src/monmove.c"
    "src/monst.c"
    "src/mplayer.c"
    "src/mthrowu.c"
    "src/muse.c"
    "src/music.c"
    "src/nhlobj.c"
    "src/nhlsel.c"
    "src/nhlua.c"
    "src/o_init.c"
    "src/objects.c"
    "src/objnam.c"
    "src/options.c"
    "src/pager.c"
    "src/pickup.c"
    "src/pline.c"
    "src/polyself.c"
    "src/potion.c"
    "src/pray.c"
    "src/priest.c"
    "src/quest.c"
    "src/questpgr.c"
    "src/read.c"
    "src/rect.c"
    "src/region.c"
    "src/restore.c"
    "src/rip.c"
    "src/rnd.c"
    "src/role.c"
    "src/rumors.c"
    "src/save.c"
    "src/sfstruct.c"
    "src/shk.c"
    "src/shknam.c"
    "src/sit.c"
    "src/sounds.c"
    "src/sp_lev.c"
    "src/spell.c"
    "src/steal.c"
    "src/steed.c"
    "src/symbols.c"
    "src/sys.c"
    "src/teleport.c"
    "src/timeout.c"
    "src/topten.c"
    "src/track.c"
    "src/trap.c"
    "src/u_init.c"
    "src/uhitm.c"
    "src/vault.c"
    "src/version.c"
    "src/vision.c"
    "src/weapon.c"
    "src/were.c"
    "src/wield.c"
    "src/windows.c"
    "src/wizard.c"
    "src/worm.c"
    "src/worn.c"
    "src/write.c"
    "src/zap.c"
    "sys/share/posixregex.c"
    "sys/share/ioctl.c"
    "sys/unix/unixunix.c"
    "sys/unix/unixmain.c"
    "sys/unix/unixres.c"
    "sys/share/unixtty.c"
    "win/tty/getline.c"
    "win/tty/termcap.c"
    "win/tty/topl.c"
    "win/tty/wintty.c")

# nethack executable.
add_executable(nethack ${NETHACK_SRC})
target_link_libraries(nethack ncurses ${TOPLUALIB} ${CMAKE_DL_LIBS} m)

# TODO: Add check-dlb, Guidebook
add_dependencies(nethack util dat lua_support)

target_include_directories(
  nethack PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/include ${LUA_INCLUDE}
                 ${NH_INC_GEN} /usr/local/include)
